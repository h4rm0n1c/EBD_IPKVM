; ADB PIO helpers (RX pulse width capture + TX low pulse)
.program adb_rx
; Measure low and high pulse widths between transitions, push each count.
    wait 1 pin 0     ; idle high (pin base + 0 = ADB RECV)
    wait 0 pin 0     ; falling edge
    set x, 0
    mov x, ~x
count_low:
    jmp pin low_done
    jmp x-- count_low
low_done:
    mov isr, x
    push noblock
    set x, 0
    mov x, ~x
count_high:
    jmp pin high_keep
    jmp high_done
high_keep:
    jmp x-- count_high
high_done:
    mov isr, x
    push noblock
    set x, 0
    mov x, ~x
    jmp count_low

.program adb_tx
; Drive line low for N cycles (N pulled from TX FIFO).
; ULN2803 inverts: drive pin high to pull bus low, float to release.
    pull block
    mov x, osr
    set pindirs, 1   ; enable output
    set pins, 1      ; drive high into ULN2803 -> bus low
drive_low:
    jmp x-- drive_low
    set pindirs, 0   ; release line
    set pins, 0      ; drop output level before next pulse
    jmp 0            ; wait for next pulse
